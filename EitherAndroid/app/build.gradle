apply plugin: 'com.android.application'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.1"

    defaultConfig {
        applicationId "com.wattpad.eitherandroid"
        minSdkVersion 15
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
        ndk {
            moduleName "EitherModule"
            stl "gnustl_static"
            cFlags "-std=c++11 -fexceptions -I/usr/local/include -frtti"
        }
    }

    flavorDimensions "abi"

    productFlavors {
        x86 {
            flavorDimension "abi"
            ndkConfig.abiFilter "x86"
            // this is the flavor part of the version code.
            // It must be higher than the arm one for devices supporting
            // both, as x86 is preferred.
            versionCode = 3
        }
        arm {
            flavorDimension "abi"
            ndkConfig.abiFilter "armeabi-v7a"
            versionCode = 1
        }
        mips {
            flavorDimension "abi"
            ndkConfig.abiFilter "mips"
            // It must be higher than the arm one for devices supporting
            // both, as mips is preferred.
            versionCode = 2
        }
        fat {
            flavorDimension "abi"
            // fat binary, lowest version code to be
            // the last option
            versionCode = 0
        }
    }

    // make per-variant version code
    applicationVariants.all { variant ->
        // get the version code of each flavor
        def abiVersion = variant.productFlavors.get(0).versionCode

        // set the composite code
        variant.mergedFlavor.versionCode = abiVersion * 1000000 + defaultConfig.versionCode
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:21.0.2'
}
